type Jest {
  """
  The source directory for the project.
  """
  pub source: Directory! @defaultPath(path: "/")

  """
  The base image to use.
  """
  pub baseImageAddress: String! = "node:25-alpine"

  """
  Execute the tests
  """
  pub test(files: [String!]! = []): Void @check {
    let cmd = ["npx", "jest"] + files

    let module = currentModule.source
    let build = container.from(baseImageAddress).
      withMountedCache("/root/.npm", cacheVolume("node-modules")).
      withDirectory("/src", module).
      withWorkdir("/src").
      withExec(["npm", "install"]).
      withExec(["npm", "run", "build"]).
      directory("/src")

    base.
    withMountedDirectory("/jest", build).
    withExec(["npm", "install", "/jest"]).
    withEnvVariable("NODE_OPTIONS", "$NODE_OPTIONS --import @dagger.io/jest/register ", expand: true).
    withExec(cmd).
    sync

    null
  }

  """
  List the tests
  """
  pub list(): String {
    let cmd = ["npx", "jest", "--listTests"]

    base.
    withExec(cmd).
    stdout
  }

  let base: Container! {
    container.from(baseImageAddress).
      withMountedCache("/root/.npm", cacheVolume("node-modules")).
      withDirectory("/src", source).
      withWorkdir("/src").
      withEnvVariable("CI", "true").
      withExec(["npm", "install"])
  }
}
