type Jest {
  """
  The source directory for the project.
  """
  pub source: Directory! @defaultPath(path: "/")

  """
  The base image to use.
  """
  pub baseImageAddress: String! = "node:25-alpine"

  """
  The package manager to use.
  """
  pub packageManager: String! = "npm"

  """
  Execute the tests
  """
  pub test(files: [String!]! = []): Void @check {
    let cmd = ["npx", "jest"] + files

    let module = currentModule.source
    let build = container.from("oven/bun:1.3.0-alpine@sha256:37e6b1cbe053939bccf6ae4507977ed957eaa6e7f275670b72ad6348e0d2c11f").
      withMountedCache("/root/.npm", cacheVolume("library-node-modules")).
      withDirectory("/src", module).
      withWorkdir("/src").
      withExec(["bun", "install"]).
      withExec(["bun", "run", "build"]).
      directory("/src")

    let packageType = container.from("alpine:3").
      withExec(["apk", "add", "jq"]).
      withFile("/package.json", source.file("package.json")).
      withExec(["jq", ".type", "/package.json"]).
      stdout

    let opts = "$NODE_OPTIONS --import @dagger.io/jest/register "
    if (packageType.contains("esm")) {
      let opts = "$NODE_OPTIONS --register @dagger.io/jest/register "
    }

    let installArgs = ["install", "/jest"]
    if (packageManager == "yarn") {
      installArgs = ["add", "--dev", "/jest"]
    }

    base.
      withMountedDirectory("/jest", build).
      withExec([packageManager] + installArgs).
      withEnvVariable("NODE_OPTIONS", opts, expand: true).
      withExec(cmd).
      sync

    null
  }

  """
  List the tests
  """
  pub list(): String {
    let cmd = ["npx", "jest", "--listTests"]

    base.
    withExec(cmd).
    stdout
  }

  let base: Container! {
    container.from(baseImageAddress).
      withMountedCache("/root/.packages", cacheVolume("node-toolchain-packages")).
      withEnvVariable("npm_config_cache", "/root/.packages").
      withEnvVariable("YARN_CACHE_FOLDER", "/root/.packages").
      withEnvVariable("PNPM_HOME", "/root/.packages").
      withEnvVariable("BUN_INSTALL_CACHE_DIR", "/root/.packages").
      withDirectory("/src", source).
      withWorkdir("/src").
      withEnvVariable("CI", "true").
      withExec([packageManager, "install"])
  }
}
